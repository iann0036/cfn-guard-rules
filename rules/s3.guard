let s3_buckets = Resources.*[ Type == 'AWS::S3::Bucket' ]

rule S3_Bucket_Encryption when %s3_buckets !empty {
    %s3_buckets.Properties.BucketEncryption exists <<Buckets must enable server-side encryption>>
    %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault.SSEAlgorithm in ["aws:kms","AES256"] <<Buckets must enable server-side encryption>>
}

rule S3_Bucket_PublicAccess when %s3_buckets !empty {
    %s3_buckets.Properties.PublicAccessBlockConfiguration exists <<Buckets must use the public access block feature>>
    %s3_buckets.Properties.PublicAccessBlockConfiguration {
        BlockPublicAcls == true <<Buckets public access block must set BlockPublicAcls to true>>
        BlockPublicPolicy == true <<Buckets public access block must set BlockPublicPolicy to true>>
        IgnorePublicAcls == true <<Buckets public access block must set IgnorePublicAcls to true>>
        RestrictPublicBuckets == true <<Buckets public access block must set RestrictPublicBuckets to true>>
    }
}

rule S3_Bucket_AccessControl when %s3_buckets !empty {
    when %s3_buckets.Properties.AccessControl exists {
        %s3_buckets.Properties.AccessControl in ["Private","LogDeliveryWrite","BucketOwnerRead","BucketOwnerFullControl"] <<Buckets must not use a public ACL>>
    }
}

let s3_bucket_policies = Resources.*[ Type == 'AWS::S3::BucketPolicy' ]

rule S3_BucketPolicy_NonTLS when %s3_bucket_policies !empty {
    %s3_bucket_policies.Properties.PolicyDocument.Statement.* == {"Action":"s3:*","Condition":{"Bool":{"aws:SecureTransport":false}},"Effect":"Deny","Principal":"*","Resource":"*"} <<Bucket policies must feature a statement to enforce TLS usage>>
}
